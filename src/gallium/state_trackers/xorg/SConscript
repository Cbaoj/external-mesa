#######################################################################
# SConscript for xorg state_tracker

Import('*')

env = env.Clone()

env.Append(CPPPATH = [
    '#/src/mesa',
])

env.PkgUseModules(['DRM', 'XORG'])

if env['HAVE_KMS']:
    env.Append(CPPDEFINES = ['HAVE_LIBKMS'])
    env.PkgUseModules(['KMS'])

conf = env.Configure()

if conf.CheckHeader('X11/extensions/dpmsconst.h'):
    env.Append(CPPDEFINES = [('HAVE_XEXTPROTO_71', '1')])

conf.Finish()

st_xorg = env.ConvenienceLibrary(
    target = 'st_xorg',
    source = env.ParseSourceList('Makefile.sources', 'C_SOURCES')
)
Export('st_xorg')
